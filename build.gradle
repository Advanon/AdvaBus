buildscript {
    ext.kotlin_version = '1.2.71'
    ext.spring_cloud_version = '2.0.0.RELEASE'
    ext.spring_boot_version = '2.0.5.RELEASE'
    ext.jackson_version = '2.9.7'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

def GROUP_ID = 'com.advanon'
def ARTIFACT_ID = 'advabus'
def VERSION = '0.2.0'

group GROUP_ID
version VERSION

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.springframework.boot:spring-boot:$spring_boot_version"

    compile "org.springframework.cloud:spring-cloud-aws-autoconfigure:$spring_cloud_version"
    compile "org.springframework.cloud:spring-cloud-aws-messaging:$spring_cloud_version"

    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

jar {
    enabled = true
}

publishing {
    publications {
        AdvaBusPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version VERSION
        }
    }
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : ''
    key = project.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ''
    publications = ['AdvaBusPublication']
    pkg {
        repo = 'advanon'
        name = ARTIFACT_ID
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Advanon/AdvaBus.git'
        version {
            name = VERSION
            released = new Date()
        }
    }
}
